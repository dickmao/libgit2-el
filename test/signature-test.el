(ert-deftest signature-default ()
  (with-temp-dir path
    (init)
    (let* ((repo (libgit2-repository-open path))
           (config (libgit2-repository-config repo)))
      (libgit2-config-set-string config "user.name" "Ludwig van")
      (libgit2-config-set-string config "user.email" "somewhere@bonn.de")
      (let ((sig (libgit2-signature-default repo)))
        (should (libgit2-signature-p sig))
        (should (string= "Ludwig van" (libgit2-signature-name sig)))
        (should (string= "somewhere@bonn.de" (libgit2-signature-email sig)))))))

(ert-deftest signature-now ()
  (let ((sig (libgit2-signature-now "Ludwig van Beethoven" "somewhere@bonn.de")))
    (should (libgit2-signature-p sig))
    (should (string= "Ludwig van Beethoven" (libgit2-signature-name sig)))
    (should (string= "somewhere@bonn.de" (libgit2-signature-email sig)))))

(ert-deftest signature-new ()
  (let* ((time (decode-time (current-time)))
         (sig (libgit2-signature-new "a" "b" time)))
    (should (equal (libgit2-signature-time sig) time))))

(ert-deftest signature-from-string ()
  (let* ((str "Ludwig van Beethoven <somewhere@bonn.de> 123456 +0100")
         (sig (libgit2-signature-from-string str)))
    (should (string= "Ludwig van Beethoven" (libgit2-signature-name sig)))
    (should (string= "somewhere@bonn.de" (libgit2-signature-email sig)))
    ;; 2^16 * 1 + 57920 = 123456
    (should (equal '(1 57920) (apply 'encode-time (libgit2-signature-time sig))))
    (should (= 3600 (car (last (libgit2-signature-time sig)))))))
